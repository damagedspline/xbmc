
set(LIBNAME libmfx)

file(GLOB VERSION_FILE ${CMAKE_SOURCE_DIR}/tools/depends/target/${LIBNAME}/LIBMFX-VERSION)
file(STRINGS ${VERSION_FILE} VER)

string(REGEX MATCH "VERSION=[^ ]*$.*" LIBMFX_VER "${VER}")
list(GET LIBMFX_VER 0 LIBMFX_VER)
string(SUBSTRING "${LIBMFX_VER}" 8 -1 LIBMFX_VER)

string(REGEX MATCH "BASE_URL=([^ ]*)" LIBMFX_BASE_URL "${VER}")
list(GET LIBMFX_BASE_URL 0 LIBMFX_BASE_URL)
string(SUBSTRING "${LIBMFX_BASE_URL}" 9 -1 LIBMFX_BASE_URL)

set(LIBMFX_URL ${LIBMFX_BASE_URL}/archive/${LIBMFX_VER}.tar.gz)

if(CORE_SYSTEM_NAME STREQUAL windowsstore)
  set(LIBMFX_ADDITIONAL_ARGS "-DCMAKE_SYSTEM_NAME=${CMAKE_SYSTEM_NAME}" "-DCMAKE_SYSTEM_VERSION=${CMAKE_SYSTEM_VERSION}")
endif()

set(LIBMFX_LIBRARY ${CMAKE_BINARY_DIR}/${CORE_BUILD_DIR}/${LIBNAME}/lib/${LIBNAME}${CMAKE_STATIC_LIBRARY_SUFFIX})
set(LIBMFX_INCLUDE_DIR ${CMAKE_BINARY_DIR}/${CORE_BUILD_DIR}/${LIBNAME}/include)

ExternalProject_Add(${LIBNAME}
  URL ${LIBMFX_URL}
  DOWNLOAD_DIR ${CMAKE_BINARY_DIR}/${CORE_BUILD_DIR}/download
  DOWNLOAD_NAME ${LIBNAME}-${LIBMFX_VER}.tar.gz
  CMAKE_ARGS
    ${LIBMFX_ADDITIONAL_ARGS}
    -DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_BINARY_DIR}/${CORE_BUILD_DIR}/${LIBNAME}
  BUILD_BYPRODUCTS ${LIBMFX_LIBRARY}
)
set_target_properties(${LIBNAME} PROPERTIES FOLDER "External Projects")

set(LIBMFX_LIBRARIES ${LIBMFX_LIBRARY})
set(LIBMFX_INCLUDE_DIRS ${LIBMFX_INCLUDE_DIR})
set(LIBMFX_DEFINITIONS -DHAVE_LIBMFX=1)

set(LIBMFX_FOUND 1 CACHE BOOL "${LIBNAME} found" FORCE)
